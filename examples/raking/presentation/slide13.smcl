{smcl}
{* 18 Feb 2022}{...}
{hline}
{p}{bf:Standardization to known margins in the population}{p_end}
{hline}

{center:{bf: Computing post-stratification weights for the cohort 1940-1945}}

{pstd}Lets get the observed data again and take a look at the population 
margins{p_end}
 
{* ex slide13ex1 }{...}
{cmd}
        use homogamy_allbus, clear
        tab meduc feduc if inrange(byr,1940,1945), matcell(data1940)
        use margins1940, clear
        list
{txt}{...}
{pstd}({stata "do slide13ex1.do":{it:click to run}}){p_end}


{* endex }{...}
{pstd}Lets get the data and the desired margins in Mata and compare them with 
the observed margins.{p_end}

{pstd}Notice the {cmd:'} at the end of the line starting with {cmd:col}. This 
turns the columnvector {cmd:col} into a rowvector.{p_end}
 
{* ex slide13ex2 }{...}
{cmd}
        mata
        data1940 = st_matrix("data1940")
        row = st_data((1,5),3)
        col = st_data((6,10),3)'
        n = sum(data1940)
        row = row:*n
        col = col:*n
        row
        rowsum(data1940)
        col
        colsum(data1940)
        end
{txt}{...}
{pstd}({stata "do slide13ex2.do":{it:click to run}}){p_end}


{* endex }{...}
{pstd}Now we can apply the same trick as before.{p_end}
 
{* ex slide13ex3 }{...}
{cmd}
        mata
        muhat = data1940
        muhat2 = 0:*data1940
        i = 1
        while(i<30 & mreldif(muhat2,muhat)>1e-8) {
            muhat2 = muhat
            muhat = muhat:/rowsum(muhat):*row
            muhat = muhat:/colsum(muhat):*col
            printf("{txt}iteration {res}%f {txt}relative change {res}%f\n", i, mreldif(muhat2,muhat))
            i = i + 1
        }
        data1940
        muhat
        end
{txt}{...}
{pstd}({stata "do slide13ex3.do":{it:click to run}}){p_end}


{* endex }{...}
{pstd}So if the margins in our data corresponded with the margins in the 
population then we would expect to find 475 couples with both low education, but
in our data we only found 146 such couples.{p_end}

{pstd}So a single couple with both low education in our data stands for 
475/146=3.3 observations in the table with the population margins.{p_end}

{pstd}This 3.3 is our post-stratification weight{p_end}
 
{* ex slide13ex4 }{...}
{cmd}
        use homogamy_allbus
        mata
        muhat:/data1940
        st_matrix("weights", muhat:/data1940)
        end
        gen weight = weights[meduc,feduc] if inrange(byr,1940,1945)
        tab meduc feduc if inrange(byr, 1940, 1945) [iweight=weight]
{txt}{...}
{pstd}({stata "do slide13ex4.do":{it:click to run}}){p_end}


{* endex }{...}
 
 
{* /p}{hline}
{* bottombar }{center:{view slide12.smcl:<<}   {view index.smcl:index}     }
{hline}
