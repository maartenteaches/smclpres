{smcl}
{*  7 Jan 2022}{...}
{hline}
{p}{bf:Functions in Mata} {hline 2} basics{p_end}
{hline}

{center:{bf: Make your function part of a Stata program}}

{pstd}
The users of our functions will often want to use our functions from Stata and 
not Mata

{pstd}
So we need a Stata program to interface between the users and our function

{pstd}
Moreover, there are lots of convenient tools in Stata for parcing standard 
syntax and displaying standard output
 
{* ex slide38ex1 }{...}
{cmd}
        program drop _all
        program define my_median, rclass
            version 16
            syntax varlist(numeric) 
            
            tempname res
            mata: my_median("`varlist'")
            matrix colnames `res' = `varlist'
            matrix rownames `res' = "median"
            matlist `res'
            return matrix median = `res'
        end
        
        mata:
        mata clear
        mata set matastrict on
        
        void my_median(string scalar varn )
        {
            real scalar k, i, n, index
            real rowvector median
            real colvector x
            
            varn = tokens(varn)
            k = cols(varn)
            median = J(1,k,.)
            
            for(i=1; i<=k; i++) {
                x = st_data(.,varn[i], 0)
                n = rows(x)
                _sort(x,1)
                if (mod(n,2)==1) {
                    index = ceil(n/2)
                    median[i] = x[index]
                }
                else {
                    index = floor(n/2)
                    median[i] = x[index]
                    index = index + 1
                    median[i] = (median[i] + x[index])/2
                }
            }
            
            st_matrix(st_local("res"),median)
        }
        end
        
        sysuse nlsw88, clear
        my_median tenure ttl_exp
        sum tenure ttl_exp, detail
{txt}{...}
{pstd}({stata "do slide38ex1.do":{it:click to run}}){p_end}


{* endex }{...}
 
 
{* /p}{hline}
{* bottombar }{center:{view slide37.smcl:<<}   {view index.smcl:index}   {view slide39.smcl:>>}}
{hline}
